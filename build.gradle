plugins {
    id 'groovy'
    id 'application'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

ext {
    vertx_version = '3.6.2'
    groovy_version = '2.5.4'
}

group 'com.aruistar.killcode'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = 'com.aruistar.killcode.launcher.AruisLauncher'
def mainVerticleName = 'com.aruistar.killcode.verticle.KillCodeVerticle'
def watchForChange = 'src/**/*'
def doOnChange = 'gradle classes'

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
}

repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public"
    }
    jcenter()
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }
    }
}

shadowJar {
    classifier = null
    version = null

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }

    mergeGroovyExtensionModules()
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovy_version"

    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-web-client:$vertx_version"
    compile("io.vertx:vertx-lang-groovy:$vertx_version") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    compile "io.vertx:vertx-zookeeper:$vertx_version"

    compile group: 'net.sourceforge.tess4j', name: 'tess4j', version: '4.3.0'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.25'
}

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}